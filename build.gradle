apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'maven'
apply plugin: 'signing'

group = 'de.vorb'

version = '0.0.4'
sourceCompatibility = 1.6

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.nativelibs4java:bridj:0.6.2'
  compile 'de.vorb:jleptonica:0.0.2'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// assume UTF-8 source code
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
  manifest {
    attributes 'Implementation-Title': 'JTesseract',
      'Implementation-Version': version
  }
}

artifacts {
  archives jar

  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
         name 'jtesseract'
         packaging 'jar'
         description 'jtesseract provides Java wrappers (JNA and BridJ) for Tesseract OCR'
         url 'https://github.com/pvorb/jtesseract'

         scm {
             url 'https://github.com/pvorb/jtesseract'
             connection 'scm:git://github.com/pvorb/jtesseract.git'
             developerConnection 'scm:git@github.com:pvorb/jtesseract.git'
         }

         licenses {
             license {
                 name 'The Apache Software License, Version 2.0'
                 url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                 distribution 'repo'
             }
         }

        developers {
          developer {
            id 'pvorb'
            name 'Paul Vorbach'
          }
        }
      }
    }
  }
}
